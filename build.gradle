plugins {
    id 'java'
    id 'idea'
}

var lwjglVersion = "3.3.3"
var lwjglNatives = "natives-windows"
group = 'dev.seeight'
version = '1.1'

repositories {
    mavenCentral()
}

void githubImpl(String user = "c8ff", String tag = "latest", String libName) {
    // Get location from 'dumb_libraries' environment variables,
    // Or else, use a local './lib/' path.
    def folder = System.getenv("dumb_libraries") ?: "./lib"
    // The release URL, including javadoc, jar, and/or sources.
    def repoLoc = "https://github.com/${user}/${libName}/releases/download/${tag}"

    def libPath = "${folder}/${libName}/"
    def urls = [
            "${repoLoc}/${libName}.jar", "${repoLoc}/${libName}-javadoc.jar", "${repoLoc}/${libName}-sources.jar"
    ]

    // Create library path.
    var libDir = file(libPath)
    if (!libDir.exists()) libDir.mkdirs()

    // Download or skip library files.
    for (final def url in urls) {
        def filePath = new File(libDir, url.substring(url.lastIndexOf('/') + 1))

        var file = file(filePath)
        if (file.exists()) {
            continue
        }

        // Download file url.
        new URL(url).withInputStream { input -> file.withOutputStream { out -> out << input }}
    }

    // Add to dependencies.
    this.dependencies {
        implementation fileTree(dir: "${folder}/${libName}", include: ['*.jar'])
    }
}

dependencies {
    githubImpl "simple-renderer"

    implementation "commons-io:commons-io:2.13.0"
    implementation "org.jetbrains:annotations:24.0.1"
    implementation "org.joml:joml:1.10.5"

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-nfd"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-nanovg"
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    archiveBaseName = "${project.name}"
}

sourceSets {
    version = null
}

javadoc {
    archivesBaseName = "${project.name}"
}

tasks.register('copyJar', Copy) {
    from jar, sourcesJar, javadocJar
    into "${System.getenv("dumb_libraries")}/${project.name}"
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}